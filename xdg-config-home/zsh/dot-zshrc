#!/bin/zsh

### Added by Zinit's installer, then adjusted to use XDG spec

if [[ ! -f "$XDG_DATA_HOME/zinit/zinit.git/zinit.zsh" ]]; then
    print -P "%F{33} %F{220}Installing %F{33}ZDHARMA-CONTINUUM%F{220} Initiative Plugin Manager (%F{33}zdharma-continuum/zinit%F{220})â€¦%f"
    command mkdir -p "$XDG_DATA_HOME/zinit" && command chmod g-rwX "$XDG_DATA_HOME/zinit"
    command git clone https://github.com/zdharma-continuum/zinit "$XDG_DATA_HOME/zinit/zinit.git" && \
        print -P "%F{33} %F{34}Installation successful.%f%b" || \
        print -P "%F{160} The clone has failed.%f%b"
fi

source "$XDG_DATA_HOME/zinit/zinit.git/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# Load a few important annexes, without Turbo
# (this is currently required for annexes)
zinit light-mode for \
    zdharma-continuum/zinit-annex-as-monitor \
    zdharma-continuum/zinit-annex-bin-gem-node \
    zdharma-continuum/zinit-annex-patch-dl \
    zdharma-continuum/zinit-annex-rust

### End of Zinit's installer chunk

source "${XDG_CONFIG_HOME}/zsh/fpath.zsh"

mkdir -p "${XDG_STATE_HOME}/zsh"
export HISTFILE="${XDG_STATE_HOME}/zsh/history"

() {
    local dir file
    for dir in "$XDG_CONFIG_HOME" "$xdg_config_dirs[@]"; do
        if [[ -d "$dir/zsh/zshrc.d" ]]; then
            for file in "$dir/zsh/zshrc.d"/*.zsh; do
                source "$file"
            done
        fi
    done

    # Run compinit
    # Cache expiry logic taken from https://gist.github.com/ctechols/ca1035271ad134841284
    mkdir -p "${XDG_CACHE_HOME}/zsh"
    local zcompdump="${XDG_CACHE_HOME}/zsh/zcompdump-${ZSH_VERSION}"

    autoload -Uz compinit
    if [[ -n ${zcompdump}(#qN.mh+24) ]]; then
        compinit -d "$zcompdump"
    else
        compinit -C -d "$zcompdump"
    fi
}

# For strange reasons, this must be done last
zinit light zdharma-continuum/fast-syntax-highlighting
fast-theme default &> /dev/null
fast-theme "${XDG_CONFIG_HOME}/zsh/highlighting/overlay.ini" &> /dev/null

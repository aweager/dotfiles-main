#!/bin/zsh

source "$XDG_DATA_HOME/awe/dumb-clone/dumb-clone.plugin.zsh"
export DUMB_CLONE_HOME="$XDG_DATA_HOME/dumb-clone/repos"

source "${XDG_CONFIG_HOME}/zsh/fpath.zsh"

mkdir -p "${XDG_STATE_HOME}/zsh"
export HISTFILE="${XDG_STATE_HOME}/zsh/history"

() {
    local dir file
    for dir in "$XDG_CONFIG_HOME" "$xdg_config_dirs[@]"; do
        if [[ -d "$dir/zsh/zshrc.d" ]]; then
            for file in "$dir/zsh/zshrc.d"/*.zsh; do
                source "$file"
            done
        fi
    done

    # Run compinit
    # Cache expiry logic taken from https://gist.github.com/ctechols/ca1035271ad134841284
    mkdir -p "${XDG_CACHE_HOME}/zsh"
    local zcompdump="${XDG_CACHE_HOME}/zsh/zcompdump-${ZSH_VERSION}"

    autoload -Uz compinit
    if [[ -n ${zcompdump}(#qN.mh+24) ]]; then
        compinit -d "$zcompdump"
    else
        compinit -C -d "$zcompdump"
    fi
}

# For strange reasons, this must be done last
dumb clone zdharma-continuum/fast-syntax-highlighting &&
    source "$DUMB_CLONE_HOME/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh"
fast-theme default &> /dev/null
fast-theme "${XDG_CONFIG_HOME}/zsh/highlighting/overlay.ini" &> /dev/null
